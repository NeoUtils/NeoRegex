plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

android {
    signingConfigs {
        release {

            def fileKeystoreProperties = rootProject.file("keystore.properties")

            def keystoreProperties = new Properties()

            if (fileKeystoreProperties.exists()) {
                keystoreProperties.load(new FileInputStream(fileKeystoreProperties))

                def storeFilePath = keystoreProperties['storeFile']

                if (storeFilePath != null) {
                    storeFile file(storeFilePath)
                    storePassword keystoreProperties['storePassword']
                    keyAlias keystoreProperties['keyAlias']
                    keyPassword keystoreProperties['keyPassword']
                }
            }
        }
    }
    compileSdk 31

    defaultConfig {
        applicationId "com.neo.regex"
        minSdk 21
        targetSdk 31
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    viewBinding {
        enabled = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    //Highlight lib
    implementation "com.github.NeoUtils:Highlight:1.0.4"

    //My Kotlin Utils Lin
    implementation project(":UtilsKT")

    //Kotlin utils fo Activity
    implementation "androidx.activity:activity-ktx:1.4.0"

    //Firebase BoM
    implementation platform('com.google.firebase:firebase-bom:29.0.3')

    //Firebase Analytics
    implementation 'com.google.firebase:firebase-analytics-ktx'

    //Firebase Database
    implementation 'com.google.firebase:firebase-database-ktx'

    //Firebase Crashlytics
    implementation 'com.google.firebase:firebase-crashlytics-ktx'

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
